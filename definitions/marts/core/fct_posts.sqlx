config {
  type: "table",
  schema: "aal_demo_devday_2026",
  tags: ["gold", "fct"],
  description: "Gold Layer: Unified fact table combining questions and answers with partitioning for performance",
  bigquery: {
    partitionBy: "DATE(created_at)",
    clusterBy: ["type", "owner_user_id"]
  },
  columns: {
    post_id: "Unique identifier for the post",
    created_at: "Timestamp when the post was created",
    type: "Type of post: question or answer",
    title: "Title of the post (only for questions)",
    body: "Content body of the post",
    owner_user_id: "User who created the post",
    parent_id: "Parent question ID (only for answers)"
  }
}

-- Gold Layer (Marts): Business-ready analytical tables
-- Combines related entities, applies business logic, optimized for queries

with posts_answers as (
    select * from ${ref("stg_posts_answers")}
),
posts_questions as (
    SELECT * FROM ${ref("stg_posts_questions")}
)

SELECT
    post_id,
    created_at,
    type,
    title,
    body,
    owner_user_id,
    parent_id
FROM
    posts_answers
UNION ALL
SELECT
    post_id,
    created_at,
    type,
    title,
    body,
    owner_user_id,
    parent_id
FROM
    posts_questions
