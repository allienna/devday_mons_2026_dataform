config {
  type: "table",
  schema: "aal_demo_devday_2026",
  description: "Gold Layer: User engagement dimension table with aggregated statistics",
  bigquery: {
    clusterBy: ["creation_date", "user_tenure"]
  },
  columns: {
    user_id: "Unique identifier for the user",
    age: "Age of the user",
    creation_date: "Timestamp when the user account was created",
    user_tenure: "Number of years since account creation",
    badge_count: "Total number of badges earned",
    last_badge_received_at: "Timestamp of most recent badge"
  },
  assertions: {
    uniqueKey: ["user_id"],
    nonNull: ["user_id"]
  }
}

-- Gold Layer (Marts): Business-ready analytical tables
-- Step 04: Demonstrates data quality testing with assertions

-- Step 04: Testing and data quality with assertions
-- Built-in tests via config: uniqueKey and nonNull
-- Custom assertion for business logic validation

with users as (
    select * from ${ref("stg_users")}
),
badges as (
    SELECT * FROM ${ref("stg_badges")}
)

SELECT
    users.user_id,
    users.age,
    users.creation_date,
    users.user_tenure,
    COUNT(DISTINCT badges.badge_id) AS badge_count,
    MAX(badges.award_timestamp) AS last_badge_received_at

FROM
    users
    LEFT JOIN badges ON users.user_id = badges.user_id
GROUP BY
    1, 2, 3, 4
